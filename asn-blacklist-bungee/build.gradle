/*
 * Copyright (C) 2025 _Novit_ (github.com/novitpw)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-conventions'
    id 'shadow-conventions'
    id 'xyz.jpenilla.run-waterfall' version '2.3.1'
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    implementation project(':asn-blacklist-core')
    implementation libs.caffeine, {
        exclude group: 'com.google.errorprone'
    }

    compileOnly libs.bungeecord.api

    implementation libs.adventure.api
    implementation libs.adventure.text.minimessage
    implementation libs.adventure.platform.bungeecord
}

tasks {
    runWaterfall {
        waterfallVersion("1.21")
    }

    processResources {
        filesMatching(["plugin.yml"]) {
            filter ReplaceTokens, tokens: [
                    "revision": "${project.version}".toString()
            ]
        }
    }

    shadowJar {
        relocate("net.kyori", "pw.novit.asnblacklist.libs.kyori")
        relocate("org.yaml", "pw.novit.asnblacklist.libs.yaml")
        relocate("com.fasterxml", "pw.novit.asnblacklist.libs.fasterxml")
        relocate("com.maxmind", "pw.novit.asnblacklist.libs.maxmind")
        relocate("com.github.benmanes", "pw.novit.asnblacklist.libs.benmanes")
    }
}
